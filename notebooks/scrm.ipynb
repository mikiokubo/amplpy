{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Supply Chain Risk Management Models"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 経済発注量モデル\n",
    "\n",
    "\n",
    "発注量 $Q$\n",
    "\n",
    "サイクル時間（発注周期）$T$ （$Q=d T$ の関係）\n",
    "\n",
    "総費用はサイクル時間 $T$ の関数：\n",
    "\n",
    "$$\n",
    "  f(T)= \\frac{K +h d T^2/2  +b d \\beta(T) /\\mu }{T +\\beta(T)/\\mu }\n",
    "$$\n",
    "\n",
    "$$\n",
    "  \\beta (T)= \\frac{\\lambda}{\\lambda +\\mu} \\left( 1- \\exp^{-(\\lambda+\\mu) T}   \\right) \n",
    "$$\n",
    "\n",
    "\n",
    "$\\beta (T)$ は在庫が $0$ になったときに途絶している確率\n",
    "\n",
    "$1/\\mu$ は途絶期間の期待値であるので，$f(T)$ の分母の $T +\\beta(T)/\\mu$ は1周期の期待値\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "metadata": {},
   "outputs": [],
   "source": [
    "from amplpy import AMPL, Environment, DataFrame,  register_magics\n",
    "ampl = AMPL(Environment(r'/Applications/ampl_macosx64/'))\n",
    "register_magics(store_name='_ampl_cells', ampl_object=ampl)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%ampl_eval\n",
    "# EOQ Model with Disruption\n",
    "# Copyright Log Opt Co., Ltd.\n",
    "# File Name = eoq1.MOD\n",
    "reset;\n",
    "param N := 3; #number of stages\n",
    "set Stages:=1..N;\n",
    "param K;                    \n",
    "param d; \n",
    "param h{Stages} default 0.;  \n",
    "param b{Stages} default 100.0;  \n",
    "param lambda_;\n",
    "param mu ;  \n",
    "\n",
    "var T{Stages}>=0;      #cycle time\n",
    "var beta{Stages} >=0;  #disruption probability when inventory reaches 0 \n",
    "var gamma{Stages} >=0; #disruption probability with which an upstream stage fails\n",
    "\n",
    "#minimize Cost: sum{i in Stages} \n",
    "#    (K+h[i]*d*T[i]*T[i]/2+b[i]*d*beta[i]/mu)/(T[i]+beta[i]/mu) ;\n",
    "\n",
    "minimize Cost: sum{i in Stages} \n",
    "    (K+h[i]*d*T[i]*T[i]/2+b[i]*d*gamma[i]/mu)/(T[i]+gamma[i]/mu) ;\n",
    "    \n",
    "subject to con1{i in Stages}:\n",
    "     beta[i]=lambda_/(lambda_+mu)*(1.0-exp(-(lambda_+mu)*T[i]));\n",
    "\n",
    "subject to con2{i in Stages}:\n",
    "     gamma[i]=1.0-  (prod{j in i..N} (1-beta[j]));\n",
    "\n",
    "subject to connect{i in 1..N-1}:\n",
    "     T[i] <= T[i+1];\n",
    "data;\n",
    "param K:=300;\n",
    "param d:=100; \n",
    "#param h:=10;  \n",
    "#param b:=100;\n",
    "param lambda_ :=0.05;\n",
    "param mu     := 0.2;  "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 11271.37049; feasibility error 8.8e-11\n",
      "0 iterations; 1 function evaluations\n",
      "T [*] :=\n",
      "1  0.718038\n",
      "2  0.718038\n",
      "3  0.718038\n",
      ";\n",
      "\n",
      "i = 1\n",
      "beta[i] = 0.032864\n",
      "gamma[i] = 0.0953874\n",
      "\n",
      "i = 2\n",
      "beta[i] = 0.032864\n",
      "gamma[i] = 0.0646479\n",
      "\n",
      "i = 3\n",
      "beta[i] = 0.032864\n",
      "gamma[i] = 0.032864\n",
      "\n"
     ]
    }
   ],
   "source": [
    "%%ampl_eval\n",
    "option solver knitro;\n",
    "for{i in Stages}{\n",
    "  let h[i] := (N+1-i)*10.0;\n",
    "}\n",
    "solve;\n",
    "#option display_1col 50;\n",
    "#option display_round 10;\n",
    "display T;\n",
    "for{i in Stages}{\n",
    "  #display lambda/(lambda+mu),(1.0-exp(-(lambda+mu)*T[i]));\n",
    "  #display lambda/(lambda+mu)*(1.0-exp(-(lambda+mu)*T[i]));\n",
    "  display i,beta[i],gamma[i];\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 途絶を考慮した新聞売り子モデル\n",
    "\n",
    "- $h$: 新聞 $1$部が売れ残ったときに課せられる在庫費用．\n",
    "- $b$: 新聞 $1$部が品切れしたときに課せられる品切れ費用．\n",
    "- $D(n)$: 需要が定常であると仮定したときの，$n$期分の需要量の合計を表す確率変数．\n",
    "- $\\alpha$: 調達先が途絶する確率．\n",
    "- $\\beta$: 途絶から回復する確率． \n",
    "\n",
    "推移行列\n",
    "$$\n",
    "\\left(\n",
    "\\begin{array}{cc}\n",
    "1-\\alpha & \\alpha \\\\\n",
    "\\beta & 1-\\beta \\\\\n",
    "\\end{array}\n",
    "\\right)\n",
    "$$\n",
    "\n",
    "定常分布\n",
    "- $u$: up\n",
    "- $d$: down \n",
    "$$\n",
    "(u,d)= \\left( \\frac{\\beta}{\\alpha + \\beta }, \\frac{\\alpha}{\\alpha + \\beta } \\right)\n",
    "$$\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 動的価格付けを考慮した多期間在庫モデル"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "metadata": {},
   "outputs": [],
   "source": [
    "ampl = AMPL(Environment(r'/Applications/ampl_macosx64/'))\n",
    "register_magics(store_name='_ampl_cells', ampl_object=ampl)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%ampl_eval\n",
    "# Newsboy model with pricing (multi-perid static model)\n",
    "# Copyright Log Opt Co., Ltd.\n",
    "# File Name = newsboy3.MOD\n",
    "\n",
    "param mu;\n",
    "param sigma;\n",
    "param S ;  # number of scenarios\n",
    "param T ;  # number of periods \n",
    "param h ;  # inventory holding cost\n",
    "param b ;  # backorder cost\n",
    "\n",
    "set Period :={1..T};\n",
    "set Scenario :={1..S};\n",
    "param Eps{Scenario,Period};\n",
    "param Prob{Scenario};\n",
    "\n",
    "var x{Period}>=0; #ordering amount\n",
    "var P{Period}>=0; #price \n",
    "var D{Period}>=0; #basic demand\n",
    "var Demand{Scenario,Period}>=0; #actual demand\n",
    "var Inv{Scenario,Period}>=0;\n",
    "var Backorder{Scenario,Period}>=0;\n",
    "\n",
    "maximize TotalRevenue: sum{t in Period} sum{s in Scenario} \n",
    "  Prob[s]*(Demand[s,t]*P[t] -h*Inv[s,t]-b*Backorder[s,t]);\n",
    "\n",
    "subject to DemConnect{s in Scenario, t in Period}:\n",
    "  #Demand[s,t] =D[t] +Eps[s,t];  # additive model \n",
    "  Demand[s,t] =Eps[s,t]*D[t];   # multiplicative model\n",
    "\n",
    "subject to Flow{s in Scenario,t in Period}: \n",
    "    (if t=1 then 0  else Inv[s,t-1])+\n",
    "     x[t]-Inv[s,t]+Backorder[s,t] = Demand[s,t];\n",
    "    \n",
    "subject to DemPrice{t in Period}:\n",
    "    D[t] = mu-P[t];\n",
    "    \n",
    "data;\n",
    "param mu    :=100;\n",
    "param sigma :=10;\n",
    "param S :=1000;  # number of scenarios \n",
    "param T :=1; \n",
    "param h :=1;   # inventory holding cost\n",
    "param b :=100;   # backorder cost\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2499.99888; feasibility error 4.26e-14\n",
      "2 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2483.785974; feasibility error 7.74e-12\n",
      "12 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2428.163334; feasibility error 7.75e-12\n",
      "10 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2446.790937; feasibility error 4.24e-11\n",
      "11 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2478.046512; feasibility error 6.05e-12\n",
      "14 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2459.596716; feasibility error 2.4e-11\n",
      "15 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2429.268732; feasibility error 2.66e-11\n",
      "11 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2431.644883; feasibility error 1.28e-11\n",
      "11 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2416.969327; feasibility error 1.45e-11\n",
      "11 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2536.413774; feasibility error 1.68e-12\n",
      "16 iterations; 0 function evaluations\n",
      "Artelys Knitro 12.1.0:Knitro 12.1.0: Locally optimal solution.\n",
      "objective 2592.963128; feasibility error 1.4e-11\n",
      "13 iterations; 0 function evaluations\n"
     ]
    }
   ],
   "source": [
    "%%ampl_eval\n",
    "option solver knitro;\n",
    "\n",
    "printf \"Perod,Sigma,P,D,x,TotalRevenue\\n\" > newsboy5.csv ;\n",
    "\n",
    "for{i in 0..10}{\n",
    " #let sigma:= i; #additive case\n",
    " let sigma := i*0.1;\n",
    " option seed 1;\n",
    " for{t in Period}{\n",
    "  for{s in Scenario}{\n",
    "  #let Eps[s,t] :=  Normal(0,sigma); #additive model\n",
    "  let Eps[s,t] :=  max(Normal(1,sigma),0); #multiplicative model\n",
    "  let Prob[s]   :=1.0/S;\n",
    "  }\n",
    "}     \n",
    " solve;\n",
    " printf  {t in Period}: \"%3i,%3i, %6.2f, %6.2f, %6.2f, %6.2f \\n\",\n",
    "    t,i,P[t],D[t],x[t],TotalRevenue\n",
    "    > newsboy5.csv; \n",
    "}\n",
    "close newsboy5.csv;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Newsboy model with pricing (multi-perid static CVaR model)\n",
    "# Copyright Log Opt Co., Ltd.\n",
    "# File Name = newsboycvar1.MOD\n",
    "\n",
    "param mu ;\n",
    "param sigma;\n",
    "param S ;  # number of scenarios\n",
    "param T ;  # number of periods \n",
    "param h ;  # inventory holding cost\n",
    "param b ;  # backorder cost\n",
    "\n",
    "\n",
    "set Period :={1..T};\n",
    "set Scenario :={1..S};\n",
    "param Eps{Scenario,Period};\n",
    "param Prob{Scenario};\n",
    "\n",
    "var x{Period}>=0; #ordering amount\n",
    "var P{Period}>=0; #price \n",
    "var D{Period}>=0; #basic demand\n",
    "var Demand{Scenario,Period}>=0; #actual demand\n",
    "var Inv{Scenario,Period}>=0;\n",
    "var Backorder{Scenario,Period}>=0;\n",
    "\n",
    "param beta;\n",
    "var f{Scenario};\n",
    "var V{Scenario}>=0;\n",
    "var alpha;\n",
    "\n",
    "minimize CVaR: \n",
    "alpha + 1.0/(1.0-beta)*(sum{s in Scenario} Prob[s]*V[s]);\n",
    "\n",
    "subject to CVaR1{s in Scenario}: \n",
    "f[s] = sum{t in Period} \n",
    "      (-Demand[s,t]*P[t] +h*Inv[s,t]+b*Backorder[s,t]) ;\n",
    "\n",
    "subject to CVaR2{s in Scenario}:\n",
    "V[s] >= f[s] - alpha;\n",
    "\n",
    "subject to DemConnect{s in Scenario, t in Period}:\n",
    "  Demand[s,t] =D[t] +Eps[s,t];  # additive model \n",
    "  #Demand[s,t] =Eps[s,t]*D[t];   # multiplicative model\n",
    "\n",
    "subject to Flow{s in Scenario,t in Period}: \n",
    "    (if t=1 then 0  else Inv[s,t-1])+\n",
    "     x[t]-Inv[s,t]+Backorder[s,t] = Demand[s,t];\n",
    "    \n",
    "subject to DemPrice{t in Period}:\n",
    "    D[t] = mu-P[t];\n",
    "    \n",
    "data;\n",
    "param mu    :=100;\n",
    "param sigma :=10;\n",
    "param S :=1000;  # number of scenarios \n",
    "param T :=1; \n",
    "param h :=1;   # inventory holding cost\n",
    "param b :=100;   # backorder cost\n",
    "\n",
    "param beta :=0.5;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
